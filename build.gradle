plugins {
  id 'org.sonarqube' version '3.5.0.2730'
}

apply from: 'https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/75a8575/helper.gradle'

def baseGroup = 'com.vary.tenantsecrets'
def baseVersion = '0.0.5'

subprojects {
  apply plugin: 'jacoco'

  group = baseGroup
  version = baseVersion

  repositories {
    mavenCentral()
  }

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion = '0.8.6'
  }

  jacocoTestReport {
    reports {
      xml.enabled true
    }
  }
}

gocdPlugin {
  id = baseGroup
  pluginVersion = baseVersion
  goCdVersion = '20.7.0'
  name = 'Tenant secrets plugin'
  description = 'Generates AES secrets that are bound to a specific tenant.'
  vendorName = 'adnovum'
  vendorUrl = 'https://github.com/adnovum/tenantsecrets'
  pluginProject = project(':plugin')
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'adnovum_tenantsecrets'
    property 'sonar.organization', 'adnovum'
    property 'sonar.host.url', 'https://sonarcloud.io'
  }
}

task tagRelease(type: Exec) {
  commandLine 'git', 'tag', "v${baseVersion}"
}

task pushTag(type: Exec) {
  commandLine 'git', 'push', 'origin', "v${baseVersion}"
  mustRunAfter tagRelease
}

task release {
  group 'Publishing'
  description 'Trigger release via GH Actions'
  dependsOn tagRelease, pushTag
}
