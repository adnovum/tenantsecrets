plugins {
  id 'org.sonarqube' version '3.5.0.2730'
  id 'jacoco'
}

apply from: 'https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/75a8575/helper.gradle'

def baseGroup = 'com.adnovum.tenantsecrets'
def baseVersion = '0.0.6'

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'jacoco'

  group = baseGroup
  version = baseVersion

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion = '0.8.6'
  }

  tasks.withType(JacocoReport).configureEach { enabled = false }
  tasks.withType(Test).configureEach { finalizedBy ':jacocoMergedReport' }
}

gocdPlugin {
  id = "${baseGroup}.plugin"
  pluginVersion = baseVersion
  goCdVersion = '20.7.0'
  name = 'Tenant secrets plugin'
  description = 'Generates AES secrets that are bound to a specific tenant.'
  vendorName = 'adnovum'
  vendorUrl = 'https://github.com/adnovum/tenantsecrets'
  pluginProject = project(':plugin')
}

tasks.register('jacocoMergedReport', JacocoReport) {
  subprojects {
    sourceSets sourceSets.main
    executionData files(tasks.withType(Test)).filter { it.name.endsWith('.exec') && it.exists() }
    dependsOn tasks.withType(Test)
  }

  reports {
    xml.required = true
    html.required = true
  }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'adnovum_tenantsecrets'
    property 'sonar.organization', 'adnovum'
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.coverage.jacoco.xmlReportPaths', "${(jacocoMergedReport as JacocoReport).reports.xml.outputLocation.get()}"
  }
}

subprojects {
  sonarqube {
    properties {
      property 'sonar.coverage.jacoco.xmlReportPaths', null
    }
  }
}

task tagRelease(type: Exec) {
  commandLine 'git', 'tag', "v${baseVersion}"
}

task pushTag(type: Exec) {
  commandLine 'git', 'push', 'origin', "v${baseVersion}"
  mustRunAfter tagRelease
}

task release {
  group 'Publishing'
  description 'Trigger release via GH Actions'
  dependsOn tagRelease, pushTag
}
